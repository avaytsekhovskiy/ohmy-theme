apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: 'drawer.gradle'

android {
    compileSdkVersion config.sdk.compile
    buildToolsVersion config.sdk.tools

    defaultConfig {
        applicationId config.package
        minSdkVersion config.sdk.min
        targetSdkVersion config.sdk.target
        versionCode config.build.code
        versionName config.build.name
        multiDexEnabled true

        buildConfigField 'String', drawer.who,  drawer.builderInfo()
        buildConfigField 'String', drawer.date, drawer.buildDate()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: config.package]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig = buildTypes.debug.signingConfig
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
    }

    //Options
    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        disable 'AllowBackup'
        disable 'ContentDescription'
        disable 'InvalidPackage'
        disable 'SpUsage'
        disable 'IconMissingDensityFolder'
        disable 'SelectableText'
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
        disable 'MissingPrefix'
        disable 'MissingRegistered'
    }

}

dependencies {
    /* test dependencies */
    testImplementation lib.junit
    testImplementation lib.mockitoCore
    testImplementation lib.leakCanaryNoOp

    /* Local files */
    implementation fileTree(dir: "libs", include: ["*.jar"])

    /* Kotlin */
    implementation lib.kotlinStd

    /* Support Libraries */
    implementation lib.supportCompat
    implementation lib.supportRecyclerView
    implementation lib.supportCardView
    implementation lib.supportDesign
    implementation lib.supportMultiDex

    /* Logger */
    implementation lib.slf4j
    implementation lib.logbackCore
    implementation(lib.logbackClassic) {
        exclude group: 'com.google.android', module: 'android'
    }

    /* Network */
    implementation lib.okHttp3
    implementation lib.okHttp3LoggingInterceptor
    implementation lib.retrofit2
    implementation lib.retrofit2gson
    implementation lib.retrofit2rx

    /* Dependency Injection */
    implementation lib.toothpickRuntime
    kapt lib.toothpickCompiler

    /* Moxy */
    implementation lib.moxy
    implementation lib.moxyCompat
    kapt lib.moxyCompiler
    implementation lib.cicerone

    /* Rx */
    implementation lib.rxJava2
    implementation lib.rxAndroid2
    implementation lib.rxKotlin
    implementation lib.rxRelay2

    /* Useful libraries */
    implementation lib.gson
    implementation lib.glide
    kapt lib.glide
    implementation lib.calligraphy
    implementation lib.joda
    implementation lib.noveoPreferencesRx2

    /* Custom View Components */
    implementation lib.circularProgress

    /* Debug Drawer */
    implementation project(':noveodebugdrawer')
}
